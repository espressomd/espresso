reset
set terminal postscript portrait size 12.0cm,8.0cm enhanced color dashed lw 1 "Helvetica" 10
set output "lennard-jones-potential.eps"
set encoding iso
set   autoscale                        # scale axes automatically
unset log                              # remove any log-scaling
unset title                            # remove title
unset xlabel                           # remove xlabel
unset ylabel                           # remove ylabel
unset key                              # remove any key position
unset label                            # remove any previous labels
set ytics auto                         # set ytics automatically
unset format                           # remove format
set xtics auto                         # set xtics automatically
set xlabel "r"
set ylabel "U_{LJ}(r)"
set xr [0.75:2.75]
set yr [-1.5:3.5]
set key top right
plot  0.0 title "" with line lt 1 lw 1 lc rgb "black", \
      (x < 2.5 ? 4.0*((1.0/(x-0.0))**(12.0) - (1.0/(x-0.0))**(6.0)) + 0.0 : 1./0 ) title "Typical Lennard-Jones Potential" with line lt 1 lw 2 lc rgb "blue", \
      (x < 2.0**(1.0/6.0) ? 4.0*((1.0/(x-0.0))**(12.0) - (1.0/(x-0.0))**(6.0)) + 1.0 : 1./0 ) title "Weeks-Chandler-Andersen Potential" with line lt 1 lw 2 lc rgb "red", \
      (x < 2.5 ? 4.0*(-12.0*(1.0/(x-0.0))**(13.0) + 6.0*(1.0/(x-0.0))**(7.0)) + 0.0 : 1./0 ) title "dU_{LJ}(r)/dr" with line lt 1 lw 2 lc rgb "black"
set output
